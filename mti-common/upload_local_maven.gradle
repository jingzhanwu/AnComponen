/**发布到私有服务器maven仓库*/
apply plugin: 'maven'

//打包main目录下代码和资源的 task
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
//配置需要上传到maven仓库的文件
artifacts {
    archives androidSourcesJar
}

//这几个定义也可以放在 各自module下的 gradle.properties文件中
def GROUP_ID = "com.components"
def ARTIFACT_ID = "common"
def VERSION = "1.0.0"

//上传到Maven仓库的task
uploadArchives {
    repositories {
        mavenDeployer {
            //指定maven仓库url
            repository(url: "http://localhost:8081/nexus/content/repositories/releases/") {
                //nexus登录默认用户名和密码
                authentication(userName: "admin", password: "admin123")

                pom.groupId = GROUP_ID// 唯一标识（通常为模块包名，也可以任意）
                pom.artifactId = ARTIFACT_ID // 项目名称（通常为类库模块名称，也可以任意）
                pom.version = VERSION // 版本号
            }
            //解决生成的pom 没有groupid 和版本号的问题
            pom.whenConfigured { pom ->
                pom.dependencies.forEach { dep ->
                    if (dep.getVersion() == "unspecified") {
                        println("--modify the dependenies module in pom.xml--->>" + dep.getArtifactId())
                        dep.setGroupId(GROUP_ID)
                        dep.setVersion(VERSION)
                    }
                }
            }
        }
    }
}
